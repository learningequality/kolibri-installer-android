// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
       google()
       jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}


apply plugin: 'com.android.application'


android {
    compileSdk 33
    buildToolsVersion '34.0.0'
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    }
    else {
        throw AssertionError("version.properties has not been defined")
    }

    def code = versionProps['VERSION_CODE'].toInteger()
    def name = versionProps['VERSION_NAME']

    // If we are doing a debug build, we'll end up with -debug-debug
    // so we strip that here.
    // For release builds, we'll either have -dev-release
    // or -official-release so it is more informative.
    def nameNoDebug = name.replace("-debug", "")

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33
        versionCode code
        versionName name
        manifestPlaceholders = [:]
        multiDexEnabled true
        setProperty("archivesBaseName", "kolibri-$nameNoDebug")
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        exclude 'lib/**/gdbserver'
        exclude 'lib/**/gdb.setup'
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("RELEASE_KEYSTORE") ?: "empty")
            keyAlias System.getenv("RELEASE_KEYALIAS") ?: ""
            storePassword System.getenv("RELEASE_KEYSTORE_PASSWD") ?: ""
            keyPassword System.getenv("RELEASE_KEYALIAS_PASSWD") ?: ""
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
            java {
                }
        }
    }

    aaptOptions {
        noCompress "tflite"
    }

}

dependencies {
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'androidx.work:work-multiprocess:2.7.1'
}
